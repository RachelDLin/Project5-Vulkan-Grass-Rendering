#version 450
#extension GL_ARB_separate_shader_objects : enable

#define WORKGROUP_SIZE 32
layout(local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

#define ENABLE_GRAVITY 1
#define ENABLE_RECOVERY 1
#define ENABLE_WIND 0

#define ENABLE_ORIENTATION_CULLING 1
#define ENABLE_VIEW_FRUSTUM_CULLING 1
#define ENABLE_DISTANCE_CULLING 1

layout(set = 0, binding = 0) uniform CameraBufferObject {
    mat4 view;
    mat4 proj;
} camera;

layout(set = 1, binding = 0) uniform Time {
    float deltaTime;
    float totalTime;
};

struct Blade {
    vec4 v0;
    vec4 v1;
    vec4 v2;
    vec4 up;
};

// TODO: Add bindings to:
// 1. Store the input blades
// 2. Write out the (un)culled blades
// 3. Write the total number of blades remaining

// The project is using vkCmdDrawIndirect to use a buffer as the arguments for a draw call

layout(set = 2, binding = 0) buffer InputBladesBuffer {
	Blade blades[];
} inputBlades;

layout(set = 2, binding = 1) buffer CulledBladesBuffer {
	Blade blades[];
} culledBlades;

layout(set = 2, binding = 2) buffer NumBlades {
	uint vertexCount;   // Write the number of blades remaining here
	uint instanceCount; // = 1
	uint firstVertex;   // = 0
	uint firstInstance; // = 0
} numBlades;

bool inBounds(float value, float bounds) {
    return (value >= -bounds) && (value <= bounds);
}

bool inViewFrustum(vec3 pos) {
	vec4 ndcPos = camera.proj * camera.view * vec4(pos.xyz, 1.0);
	float tolerance = 0.9;
	float homogCoord = ndcPos.w - tolerance;
	return inBounds(ndcPos.x, homogCoord) && inBounds(ndcPos.y, homogCoord);
}

// Simple hash function to generate pseudo-random float in [0,1]
float rand(uint seed) {
    seed = (seed ^ 61u) ^ (seed >> 16u);
    seed *= 9u;
    seed = seed ^ (seed >> 4u);
    seed *= 0x27d4eb2du;
    seed = seed ^ (seed >> 15u);
    return float(seed & 0xFFFFFFFu) / float(0x10000000u);
}

float fade(float t) {
    return t * t * t * (t * (t * 6.0 - 15.0) + 10.0);
}

vec2 hash2(vec2 p) {
    p = vec2(
        dot(p, vec2(127.1, 311.7)),
        dot(p, vec2(269.5, 183.3))
    );
    return normalize(-1.0 + 2.0 * fract(sin(p) * 43758.5453123));
}

float perlin(vec2 p) {
    vec2 i = floor(p);   // grid cell
    vec2 f = fract(p);   // local position

    vec2 g00 = hash2(i + vec2(0, 0));
    vec2 g10 = hash2(i + vec2(1, 0));
    vec2 g01 = hash2(i + vec2(0, 1));
    vec2 g11 = hash2(i + vec2(1, 1));

    float d00 = dot(g00, f - vec2(0, 0));
    float d10 = dot(g10, f - vec2(1, 0));
    float d01 = dot(g01, f - vec2(0, 1));
    float d11 = dot(g11, f - vec2(1, 1));

    vec2 u = vec2(fade(f.x), fade(f.y));

    return mix(
        mix(d00, d10, u.x),
        mix(d01, d11, u.x),
        u.y
    );
}

void main() {
	uint idx = gl_GlobalInvocationID.x;

	// Reset the number of blades to 0
	if (idx == 0) {
		numBlades.vertexCount = 0;
		numBlades.instanceCount = 1;
		numBlades.firstVertex = 0;
		numBlades.firstInstance = 0;
	}
	barrier(); // Wait till all threads reach this point

    // TODO: Apply forces on every blade and update the vertices in the buffer

	Blade blade = inputBlades.blades[idx];

	// inputs
	vec3 pos0 = blade.v0.xyz;
	vec3 pos1 = blade.v1.xyz;
	vec3 pos2 = blade.v2.xyz;

	float dir = blade.v0.w;
	vec3 rDir = normalize(vec3(cos(dir), 0, sin(dir)));
	vec3 uDir = normalize(blade.up.xyz);
	vec3 fDir = normalize(cross(rDir, uDir));
	
	float height = blade.v1.w;
	float width = blade.v2.w;
	
	float stiffness = blade.up.w;
	vec3 Fg = vec3(0.0);
	vec3 Fr = vec3(0.0);
	vec3 Fw = vec3(0.0);
	
	#if ENABLE_GRAVITY
		// gravity: environmental gravity + front gravity to cause the top to bend
		vec3 gE = vec3(0, -20.0, 0);
		vec3 gF = 0.25 * length(gE) * fDir;
		Fg = gE + gF;
	#endif

	#if ENABLE_RECOVERY
		// recovery
		vec3 pos2_initial = pos0 + uDir * height;
		Fr = (pos2_initial - pos2) * stiffness;
	#endif 

	#if ENABLE_WIND
		// wind
		float randNum = rand(idx + uint(totalTime * 1000.0));
		float t0 = perlin(vec2(pos0.x, pos0.z));
		float t1 = perlin(vec2(pos1.x, pos1.z));
		float t2 = perlin(vec2(pos2.x, pos2.z));

		vec3 windDir = normalize(vec3(1.0, -0.25, 1.0) + 
								vec3(0.25) + 
								vec3(0.53 * sin(2.3 * totalTime) + 0.27 * (1.0 + t0 * 0.1) * sin(5.7 * totalTime + 0.87), 
										0, 
										0.38 * sin(0.5 * totalTime) + 0.27 * (1.0 + t2 * 0.1) * sin(1.34 * totalTime + 0.13)
								)
						);
		float windStrength = 5.0 + (t1 * randNum);
		float dirAlignment = 1.0 - abs(dot(windDir, normalize(pos2 - pos0)));
		float heightRatio = dot(pos2 - pos0, uDir) / height;
		float alignment = heightRatio * dirAlignment;
		Fw = windStrength * windDir * alignment;
	#endif

	// sum forces
	vec3 F = Fg + Fr + Fw;

	///// apply force to blade

	// new v2 pos
	vec3 newPos2 = deltaTime * F + pos2;
	
	// new v1 pos
	float lProj = length(pos2 - pos0 - uDir * dot(pos2 - pos0, uDir));
	vec3 newPos1 = pos0 + height * uDir * max(1.0 - (lProj / height), 0.05 * max(lProj / height, 1));

	// blade length
	float L0 = length(pos1 - pos0);
	float L1 = length(pos2 - pos1);
	float numCtrlPts = 2.0;
	float L = (2 * L0 + (numCtrlPts - 1.0) * L1) / (numCtrlPts + 1.0);

	// height to length ratio
	float r = height / L;
	vec3 corrPos1 = pos0 + r * (newPos1 - pos0);
	vec3 corrPos2 = corrPos1 + r * (newPos2 - newPos1);

	// correct v1 and v2 positions
	blade.v1.xyz = corrPos1;
	blade.v2.xyz = corrPos2;

	inputBlades.blades[idx] = blade;
	
	// TODO: Cull blades that are too far away or not in the camera frustum and write them
	// to the culled blades buffer
	// Note: to do this, you will need to use an atomic operation to read and update numBlades.vertexCount
	// You want to write the visible blades to the buffer without write conflicts between threads

	mat4 cam_invView = inverse(camera.view);
	vec3 cam_F = vec3(cam_invView[2]);

	#if ENABLE_ORIENTATION_CULLING
		// orientation culling
		if (abs(dot(cam_F, fDir)) > 0.9) {
			return;
		}
	#endif

	#if ENABLE_VIEW_FRUSTUM_CULLING
		// view_frustum culling
		vec3 m = 0.25 * pos0 + 0.5 * pos1 + 0.25 * pos2;
		if (!inViewFrustum(pos0) && !inViewFrustum(pos2) && !inViewFrustum(m)) {
			return;
		}
	#endif

	#if ENABLE_DISTANCE_CULLING
		// distance 
		vec3 cam_pos = vec3(cam_invView[3]);
		float dProj = length(pos0 - cam_pos - uDir * dot(pos0 - cam_pos, uDir));
		float dMax = 20.0; // culling distance
		float lodLvl = 1;
		if (mod(idx, lodLvl) > floor(lodLvl * (1.0 - dProj / dMax))) {
			return;
		}
	#endif

	// copy unculled blades into culledBlades buffer
	if (idx < (1 << 13)) {
		uint newIdx = atomicAdd(numBlades.vertexCount, 1);
		culledBlades.blades[newIdx] = blade;
	}
}
